apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.mikepenz.aboutlibraries.plugin'

if (file('./token.gradle').exists()) {
    apply from: 'token.gradle'
}

android {
    compileSdk 31
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    defaultConfig {
        applicationId "com.wa2c.android.medoly.plugin.action.tweet"
        minSdkVersion 19
        targetSdkVersion 31
        versionCode 17
        versionName "2.5.1"

        archivesBaseName = "MedolyTwitterPlugin_${versionName}"

        buildConfigField("String", "K1", "\"\"")
        buildConfigField("String", "K2", "\"\"")
        buildConfigField("String", "T1", "\"\"")
        buildConfigField("String", "T2", "\"\"")

        multiDexEnabled true
    }

    buildTypes {
        debug {
            versionNameSuffix 'd'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
        }
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:deprecation"
        }
    }

    signingConfigs {
        debug {
            storeFile file("../debug.keystore")
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
    }

    lintOptions {
        lintConfig file('lint.xml')
    }

    buildFeatures {
        dataBinding = true
    }

}

repositories {
    google()
    mavenCentral()
    maven { url 'https://github.com/wa2c/medoly-library/raw/master/repository/' }
    maven { url 'https://github.com/wa2c/prefs/raw/master/repository/' }
}

dependencies {
    // Medoly library
    implementation 'com.wa2c.android:medolylibrary:1.3.1'
    // Prefs
    implementation 'com.wa2c.android:prefs:0.1.2'

    // Support Library
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation "androidx.work:work-runtime-ktx:2.7.1"
    implementation "com.google.android.material:material:1.5.0"

    // Kotlin
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.0'

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:29.0.4')
    implementation 'com.google.firebase:firebase-core'
    implementation 'com.google.firebase:firebase-crashlytics'

    api 'org.twitter4j:twitter4j-core:4.0.7'
    implementation 'com.twitter.twittertext:twitter-text:3.1.0'
    implementation ('asia.ivity.android:drag-sort-listview:1.0') {
        exclude module: 'support-v4'
    }
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation "com.mikepenz:aboutlibraries:${about_libs_version}"
}

